#file: ./money/main

/scoreboard players enable @a[scores={money=1..}] pay_money
/scoreboard players reset @a[scores={money=..0}] pay_money
/scoreboard players enable @a check_money

as(@a[scores={check_money=1..}]) {
  /function unnamedde:money/check
}

asat(@a[scores={pay_money=1..}]) {
  if('entity @p[distance=.1..]') {
    if('score @s money >= @s pay_money') {
      /function unnamedde:money/pay
    } else {
      /tellraw @s [{"text":"You don't have enough money","color":"dark_red"}]
    }
  } else {
    /tellraw @s [{"text":"There's no other player online :(","color":"dark_red"}]
  }
}
/scoreboard players reset @a[scores={pay_money=1..}] pay_money

#file: ./money/join
if('score money ud_settings matches 1..') {
  if(!'score @s money matches 0..') {
    /scoreboard players operation @s money = #startValue money
  }
  /tellraw @s [{"text":"Your starting balance is: "},{"score":{"name":"@s","objective":"money"},"color":"gold"},{"text":"$","color":"gold"}]
}


#file: ./money/check

/scoreboard players reset @s check_money
/tellraw @s [{"text":"Your balance is: "},{"score":{"name":"@s","objective":"money"},"color":"gold"},{"text":"$","color":"gold"}]

#file: ./money/pay

/scoreboard players operation @s money -= @s pay_money
/scoreboard players operation @p[distance=.1..] money += @s pay_money
/playsound minecraft:entity.player.levelup player @p[distance=.1..]
/tellraw @s [{"text":"Paid "},{"selector":"@p[distance=.1..]","color":"aqua"},{"text":" "},{"score":{"name":"@s","objective":"pay_money"},"color":"gold"},{"translate":"$","color":"gold"}]
/tellraw @p[distance=.1..] [{"text":"You received "},{"score":{"name":"@s","objective":"pay_money"},"color":"gold"},{"text":"$","color":"gold"},{"text":" from "},{"selector":"@s","color":"aqua"}]
